apply plugin: 'com.android.application'
//apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'org.greenrobot.greendao'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.wair.accountbook"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 9
        versionName "1.1"
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }

        flavorDimensions "versionCode"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        'eventBusIndex': "com.github.airsaid.accountbook.EventBusIndex"
                ]
            }
        }
    }

    greendao {
        schemaVersion 1
        daoPackage 'com.github.airsaid.accountbook.data'
        targetGenDir 'src/main/java'
    }

    productFlavors {
        xiaomi {}
        huawei {}
        wandoujia {}
        vivo {}
        baidu {}
        oppo {}
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                    "leancloud_appid" : properties.getProperty("leancloud.appid"),
                    "leancloud_appkey": properties.getProperty("leancloud.appkey"),
                    "bugly_appid"     : properties.getProperty("bugly.appid")
            ]
        }
        debug {
            minifyEnabled false
            manifestPlaceholders = [
                    "leancloud_appid" : properties.getProperty("leancloud.appid"),
                    "leancloud_appkey": properties.getProperty("leancloud.appkey"),
                    "bugly_appid"     : properties.getProperty("bugly.appid")
            ]
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    // 为了解决部分第三方库重复打包了META-INF的问题
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    api 'com.android.support:appcompat-v7:27.1.1'
    // Material design 兼容库
    api 'com.android.support:design:27.1.1'
    // CardView 支持库
    api 'com.android.support:cardview-v7:27.1.1'
    // 取色库
    api 'com.android.support:palette-v7:27.1.1'
    // LeanCloud 基础包
//    api 'cn.leancloud.android:avoscloud-sdk:v3.+'
    api 'cn.leancloud.android:avoscloud-sdk:v4.7.3'
    // retrofit2 网络请求库
    api 'com.squareup.retrofit2:retrofit:2.4.0'
    // Gson 解析库
    api 'com.squareup.retrofit2:converter-gson:2.4.0'
    // Glide 图片加载库
    api 'com.github.bumptech.glide:glide:3.7.0'
    // 控件注解库
    api 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    // 消息传递库
    api 'org.greenrobot:eventbus:3.1.1'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'
    // 转场动画库
    api 'me.immortalz:transitionhelper:1.0.6'
    // RecyclerView Adapter 库
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.34'
    // MD 风格输入 Dialog
    api 'com.github.StephaneBg:SimpleNumberPicker:1.3'
    // 图片选择库
    api 'com.github.LuckSiege.PictureSelector:picture_library:v1.4.6'
    // 腾讯 Bugly
    api 'com.tencent.bugly:crashreport_upgrade:1.3.5'
    api 'com.tencent.bugly:nativecrashreport:3.3.1'
    // greenDAO 数据库框架
    api 'org.greenrobot:greendao:3.2.2'
    // 图表库
    api 'com.github.PhilJay:MPAndroidChart:v3.0.2'
}
